plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'checkstyle'
    id 'com.diffplug.spotless' version '7.0.1' // Apply the Spotless plugin
}

group = 'com.test'
version = '0.0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('copyLatestJar', Copy) {
    dependsOn 'build'  // Ensure the build task runs first

    from("${buildDir}/libs")
    into("${buildDir}/libs/latest")
    include('*.jar')
    exclude('*-plain.jar')

	doLast {
        println("Build and copy of jar is done.")
    }
}

tasks.register('buildAndCopy') {
    dependsOn 'build', 'copyLatestJar'
    doLast {
        println 'Build and copy completed!'
    }
}

checkstyle {
    toolVersion = '10.12.1' // Specify the Checkstyle version
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source ='src/main/java'
}

checkstyleTest {
    source ='src/test/java'
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

// Spotless Configuration
spotless {
    java {
        target 'src/**/*.java' // Target all Java files
        googleJavaFormat('1.17.0') // Use Google Java Format
        importOrder() // Sort imports
        removeUnusedImports() // Remove unused imports
        trimTrailingWhitespace() // Trim trailing whitespace
        endWithNewline() // Ensure file ends with a newline
    }
}